apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'org.jetbrains.dokka-android'

// Bintray upload
group = groupName
version = versionName

def SITE_URL = 'https://github.com/inqbarna/kotpref'
def GIT_URL = 'https://github.com/inqbarna/kotpref.git'


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

dokka {
    outputFormat = 'javadoc'
    moduleName = project.name
    linkMapping {
        dir = "${project.name}/src/main/kotlin"
        url = "https://github.com/chibatching/Kotpref/tree/v${version}/${project.name}/src/main/kotlin"
        suffix = "#L"
    }
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            artifactId archivesBaseName
            name moduleName

            packaging 'aar'
            url SITE_URL

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'chibatching'
                    name 'Takao Chiba'
                    email 'chibatching.apps@gmail.com'
                }
            }
            scm {
                connection GIT_URL
                developerConnection GIT_URL
                url SITE_URL
            }
        }
    }
}
